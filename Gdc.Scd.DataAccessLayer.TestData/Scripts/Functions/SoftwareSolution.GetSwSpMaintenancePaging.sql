IF OBJECT_ID('SoftwareSolution.GetSwSpMaintenancePaging') IS NOT NULL
  DROP FUNCTION SoftwareSolution.GetSwSpMaintenancePaging;
go


CREATE FUNCTION SoftwareSolution.GetSwSpMaintenancePaging (
    @approved bit,
    @digit dbo.ListID readonly,
    @av dbo.ListID readonly,
    @year dbo.ListID readonly,
    @lastid bigint,
    @limit int
)
RETURNS @tbl TABLE 
        (   
            rownum int NOT NULL,
            Id bigint NOT NULL,
            Pla bigint NOT NULL,
            Sfab bigint NOT NULL,
            Sog bigint NOT NULL,
            SwDigit bigint NOT NULL,
            Availability bigint NOT NULL,
            Year bigint NOT NULL,
            [2ndLevelSupportCosts] float NULL,
            InstalledBaseSog float NULL,
            TotalInstalledBaseSog float NULL,
            ReinsuranceFlatfee float NULL,
            CurrencyReinsurance bigint NULL,
            RecommendedSwSpMaintenanceListPrice float NULL,
            MarkupForProductMarginSwLicenseListPrice float NULL,
            ShareSwSpMaintenanceListPrice float NULL,
            DiscountDealerPrice float NULL
        )
AS
BEGIN
        declare @isEmptyDigit    bit = Portfolio.IsListEmpty(@digit);
        declare @isEmptyAV    bit = Portfolio.IsListEmpty(@av);
        declare @isEmptyYear    bit = Portfolio.IsListEmpty(@year);

        if @limit > 0
        begin
            with cte as (
                select ROW_NUMBER() over(
                            order by ssm.SwDigit
                                   , ya.AvailabilityId
                                   , ya.YearId
                        ) as rownum
                      , ssm.*
                      , ya.AvailabilityId
                      , ya.YearId
                FROM SoftwareSolution.SwSpMaintenance ssm
                JOIN Dependencies.Duration_Availability ya on ya.Id = ssm.DurationAvailability
                WHERE (@isEmptyDigit = 1 or ssm.SwDigit in (select id from @digit))
                    AND (@isEmptyAV = 1 or ya.AvailabilityId in (select id from @av))
                    AND (@isEmptyYear = 1 or ya.YearId in (select id from @year))
                    and ssm.DeactivatedDateTime is null
            )
            insert @tbl
            select top(@limit)
                    rownum
                  , ssm.Id
                  , ssm.Pla
                  , ssm.Sfab
                  , ssm.Sog
                  , ssm.SwDigit
                  , ssm.AvailabilityId
                  , ssm.YearId
              
                  , case when @approved = 0 then ssm.[2ndLevelSupportCosts] else ssm.[2ndLevelSupportCosts_Approved] end
                  , case when @approved = 0 then ssm.InstalledBaseSog else ssm.InstalledBaseSog_Approved end
                  , case when @approved = 0 then ssm.TotalIB else ssm.TotalIB_Approved end

                  , case when @approved = 0 then ssm.ReinsuranceFlatfee else ssm.ReinsuranceFlatfee_Approved end
                  , case when @approved = 0 then ssm.CurrencyReinsurance else ssm.CurrencyReinsurance_Approved end
                  , case when @approved = 0 then ssm.RecommendedSwSpMaintenanceListPrice else ssm.RecommendedSwSpMaintenanceListPrice_Approved end
                  , case when @approved = 0 then ssm.MarkupForProductMarginSwLicenseListPrice else ssm.MarkupForProductMarginSwLicenseListPrice_Approved end
                  , case when @approved = 0 then ssm.ShareSwSpMaintenanceListPrice else ssm.ShareSwSpMaintenanceListPrice_Approved end
                  , case when @approved = 0 then ssm.DiscountDealerPrice else ssm.DiscountDealerPrice_Approved end

            from cte ssm where rownum > @lastid
        end
    else
        begin
            insert @tbl
            select -1 as rownum
                  , ssm.Id
                  , ssm.Pla
                  , ssm.Sfab
                  , ssm.Sog
                  , ssm.SwDigit
                  , ya.AvailabilityId
                  , ya.YearId

                  , case when @approved = 0 then ssm.[2ndLevelSupportCosts] else ssm.[2ndLevelSupportCosts_Approved] end
                  , case when @approved = 0 then ssm.InstalledBaseSog else ssm.InstalledBaseSog_Approved end
                  , case when @approved = 0 then ssm.TotalIB else ssm.TotalIB_Approved end

                  , case when @approved = 0 then ssm.ReinsuranceFlatfee else ssm.ReinsuranceFlatfee_Approved end
                  , case when @approved = 0 then ssm.CurrencyReinsurance else ssm.CurrencyReinsurance_Approved end
                  , case when @approved = 0 then ssm.RecommendedSwSpMaintenanceListPrice else ssm.RecommendedSwSpMaintenanceListPrice_Approved end
                  , case when @approved = 0 then ssm.MarkupForProductMarginSwLicenseListPrice else ssm.MarkupForProductMarginSwLicenseListPrice_Approved end
                  , case when @approved = 0 then ssm.ShareSwSpMaintenanceListPrice else ssm.ShareSwSpMaintenanceListPrice_Approved end
                  , case when @approved = 0 then ssm.DiscountDealerPrice else ssm.DiscountDealerPrice_Approved end

            FROM SoftwareSolution.SwSpMaintenance ssm
            JOIN Dependencies.Duration_Availability ya on ya.Id = ssm.DurationAvailability

            WHERE (@isEmptyDigit = 1 or ssm.SwDigit in (select id from @digit))
                AND (@isEmptyAV = 1 or ya.AvailabilityId in (select id from @av))
                AND (@isEmptyYear = 1 or ya.YearId in (select id from @year))
                and ssm.DeactivatedDateTime is null

        end

    RETURN;
END
GO