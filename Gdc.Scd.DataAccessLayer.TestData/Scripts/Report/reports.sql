IF OBJECT_ID('dbo.HasScdAdminRole') IS NOT NULL
  DROP FUNCTION dbo.HasScdAdminRole;
go 

IF OBJECT_ID('dbo.HasRole') IS NOT NULL
  DROP FUNCTION dbo.HasRole;
go 

CREATE FUNCTION dbo.HasRole(@login nvarchar(255), @role nvarchar(255))
RETURNS bit
AS
BEGIN

    declare @result bit = 0;

    if exists(select * from dbo.UserRole ur
                where ur.UserId = (select id from dbo.[User] where Login = @login)
                      and ur.RoleId = (select id from dbo.Role where UPPER(Name) = UPPER(@role)))
       set @result = 1;
   
    RETURN @result;

END

go

CREATE FUNCTION dbo.HasScdAdminRole(@login nvarchar(255))
RETURNS bit
AS
BEGIN
    return dbo.HasRole(@login, 'SCD ADMIN');
END

go

IF OBJECT_ID('Report.GetCosts') IS NOT NULL
  DROP FUNCTION Report.GetCosts;
go 

IF OBJECT_ID('Report.GetCostsFull') IS NOT NULL
  DROP FUNCTION Report.GetCostsFull;
go 

IF OBJECT_ID('InputAtoms.CountryView', 'V') IS NOT NULL
  DROP VIEW InputAtoms.CountryView;
go

CREATE VIEW InputAtoms.CountryView WITH SCHEMABINDING AS
    select c.Id
         , c.Name
         , c.ISO3CountryCode
         , c.IsMaster
         , c.SAPCountryCode
         , cg.Id as CountryGroupId
         , cg.Name as CountryGroup
         , cg.LUTCode
         , cr.Id as ClusterRegionId
         , cr.Name as ClusterRegion
         , r.Id as RegionId
         , r.Name as Region
         , cur.Id as CurrencyId
         , cur.Name as Currency
    from InputAtoms.Country c
    left join InputAtoms.CountryGroup cg on cg.Id = c.CountryGroupId
    left join InputAtoms.Region r on r.Id = c.RegionId
    left join InputAtoms.ClusterRegion cr on cr.Id = c.ClusterRegionId
    left join [References].Currency cur on cur.Id = c.CurrencyId

GO

IF OBJECT_ID('Portfolio.IntToListID') IS NOT NULL
  DROP FUNCTION Portfolio.IntToListID;
go 

CREATE FUNCTION Portfolio.IntToListID(@var bigint)
RETURNS @tbl TABLE( id bigint NULL)
AS
BEGIN
    insert @tbl(id) values (@var)
RETURN
END
GO


IF OBJECT_ID('[Report].[GetCosts]') IS NOT NULL
    DROP FUNCTION [Report].[GetCosts]
GO

CREATE FUNCTION [Report].[GetCosts]
(
    @cnt bigint,
    @wg bigint,
    @av bigint,
    @dur bigint,
    @reactiontime bigint,
    @reactiontype bigint,
    @loc bigint,
    @pro bigint
)
RETURNS @tbl TABLE (
           Id                                bigint
         , Fsp                               nvarchar(255)
         , FspDescription                    nvarchar(255)
         , CountryId                         bigint
         , Country                           nvarchar(255)
         , CurrencyId                        bigint
         , ExchangeRate                      float
         , WgId                              bigint
         , Wg                                nvarchar(255)
         , AvailabilityId                    bigint
         , Availability                      nvarchar(255)
         , DurationId                        bigint
         , Duration                          nvarchar(255)
         , Year                              int
         , IsProlongation                    bit
         , ReactionTimeId                    bigint
         , ReactionTime                      nvarchar(255)
         , ReactionTypeId                    bigint
         , ReactionType                      nvarchar(255)
         , ServiceLocationId                 bigint
         , ServiceLocation                   nvarchar(255)
         , ProActiveSlaId                    bigint
         , ProActiveSla                      nvarchar(255)
         , Sla                               nvarchar(255)
         , SlaHash                           int
         , StdWarranty                       float
         , AvailabilityFee                   float
         , TaxAndDutiesW                     float
         , TaxAndDutiesOow                   float
         , Reinsurance                       float
         , ProActive                         float
         , ServiceSupportCost                float
         , MaterialW                         float
         , MaterialOow                       float
         , FieldServiceCost                  float
         , Logistic                          float
         , OtherDirect                       float
         , LocalServiceStandardWarranty      float
         , Credits                           float
         , ServiceTC                         float
         , ServiceTP                         float
         , ServiceTC1                        float
         , ServiceTC2                        float
         , ServiceTC3                        float
         , ServiceTC4                        float
         , ServiceTC5                        float
         , ServiceTC1P                       float
         , ServiceTP1                        float
         , ServiceTP2                        float
         , ServiceTP3                        float
         , ServiceTP4                        float
         , ServiceTP5                        float
         , ServiceTP1P                       float
         , ListPrice                         float
         , DealerDiscount                    float
         , DealerPrice                       float
         , ServiceTCManual                   float
         , ServiceTPManual                   float
         , ServiceTP_Released                float
         , ChangeUserName                    nvarchar(255)
         , ChangeUserEmail                   nvarchar(255)
)
AS
begin

    declare @cntTable dbo.ListId; insert into @cntTable(id) values(@cnt);

    declare @wgTable dbo.ListId; if @wg is not null insert into @wgTable(id) values(@wg);

    declare @avTable dbo.ListId; if @av is not null insert into @avTable(id) values(@av);

    declare @durTable dbo.ListId; if @dur is not null insert into @durTable(id) values(@dur);

    declare @rtimeTable dbo.ListId; if @reactiontime is not null insert into @rtimeTable(id) values(@reactiontime);

    declare @rtypeTable dbo.ListId; if @reactiontype is not null insert into @rtypeTable(id) values(@reactiontype);

    declare @locTable dbo.ListId; if @loc is not null insert into @locTable(id) values(@loc);

    declare @proTable dbo.ListId; if @pro is not null insert into @proTable(id) values(@pro);
    
    insert into @tbl 
    select 
              c.Id                               
            , fsp.Name as Fsp                              
            , fsp.ServiceDescription as FspDescription                   
            , c.CountryId                        
            , c.Country                          
            , c.CurrencyId                       
            , c.ExchangeRate                     
            , c.WgId                             
            , c.Wg                               
            , c.AvailabilityId                   
            , c.Availability                     
            , c.DurationId                       
            , c.Duration                         
            , c.Year                             
            , c.IsProlongation                   
            , c.ReactionTimeId                   
            , c.ReactionTime                     
            , c.ReactionTypeId                   
            , c.ReactionType                     
            , c.ServiceLocationId                
            , c.ServiceLocation                  
            , c.ProActiveSlaId                   
            , c.ProActiveSla                     
            , c.Sla                              
            , c.SlaHash                          
            , c.StdWarranty                      
            , c.AvailabilityFee                  
            , c.TaxAndDutiesW                    
            , c.TaxAndDutiesOow                  
            , c.Reinsurance                      
            , c.ProActive                        
            , c.ServiceSupportCost               
            , c.MaterialW                        
            , c.MaterialOow                      
            , c.FieldServiceCost                 
            , c.Logistic                         
            , c.OtherDirect                      
            , c.LocalServiceStandardWarranty     
            , c.Credits                          
            , c.ServiceTC                        
            , c.ServiceTP                        
            , c.ServiceTC1                       
            , c.ServiceTC2                       
            , c.ServiceTC3                       
            , c.ServiceTC4                       
            , c.ServiceTC5                       
            , c.ServiceTC1P                      
            , c.ServiceTP1                       
            , c.ServiceTP2                       
            , c.ServiceTP3                       
            , c.ServiceTP4                       
            , c.ServiceTP5                       
            , c.ServiceTP1P                      
            , c.ListPrice                        
            , c.DealerDiscount                   
            , c.DealerPrice                      
            , c.ServiceTCManual                  
            , c.ServiceTPManual                  
            , c.ServiceTP_Released               
            , c.ChangeUserName                   
            , c.ChangeUserEmail                  
    from Hardware.GetCosts(1, @cntTable, @wgTable, @avTable, @durTable, @rtimeTable, @rtypeTable, @locTable, @proTable, null, null) c
    left join Fsp.HwFspCodeTranslation fsp on fsp.SlaHash = c.SlaHash and fsp.Sla = c.Sla    
    return;
end
GO

IF OBJECT_ID('Report.GetReportColumnTypeByName') IS NOT NULL
  DROP FUNCTION Report.GetReportColumnTypeByName;
go 

CREATE FUNCTION Report.GetReportColumnTypeByName
(
    @name nvarchar(max)
)
RETURNS bigint 
AS
BEGIN
	DECLARE @Id bigint
    select @Id=id from Report.ReportColumnType where Name=@name
	RETURN @Id
END
GO

IF OBJECT_ID('Report.GetReportFilterTypeByName') IS NOT NULL
  DROP FUNCTION Report.GetReportFilterTypeByName;
go 

CREATE FUNCTION Report.GetReportFilterTypeByName
(
    @name nvarchar(max),
	@multi bit
)
RETURNS bigint 
AS
BEGIN
	DECLARE @Id bigint
    select @Id= id from Report.ReportFilterType where MultiSelect = @multi and name = @name
	RETURN @Id
END
GO

if OBJECT_ID('[Report].[GetParameterHw]') is not null
    drop function [Report].[GetParameterHw];
GO

create function [Report].[GetParameterHw]
(
    @approved bit,
    @cnt bigint,
    @wg bigint,
    @av bigint,
    @duration     bigint,
    @reactiontime bigint,
    @reactiontype bigint,
    @loc bigint,
    @pro bigint
)
RETURNS TABLE 
AS
RETURN (
    with CountryCte as (
        select c.*
             , cur.Name as Currency
             , er.Value as ExchangeRate
             , case when @approved = 0 then tax.TaxAndDuties else tax.TaxAndDuties_Approved end as TaxAndDuties
        from InputAtoms.Country c 
        INNER JOIN [References].Currency cur on cur.Id = c.CurrencyId
        INNER JOIN [References].ExchangeRate er on er.CurrencyId = c.CurrencyId
        LEFT JOIN Hardware.TaxAndDuties tax on tax.Country = c.Id and tax.DeactivatedDateTime is null
        where c.Id = @cnt
    )
    , WgCte as (
        select wg.Id
             , wg.Name
             , wg.Description
             , wg.SCD_ServiceType
             , pla.ClusterPlaId
             , sog.Description as SogDescription
             , wg.RoleCodeId
        
             , case when @approved = 0 then afr.AFR1                           else AFR1_Approved                               end as AFR1 
             , case when @approved = 0 then afr.AFR2                           else AFR2_Approved                               end as AFR2 
             , case when @approved = 0 then afr.AFR3                           else afr.AFR3_Approved                           end as AFR3 
             , case when @approved = 0 then afr.AFR4                           else afr.AFR4_Approved                           end as AFR4 
             , case when @approved = 0 then afr.AFR5                           else afr.AFR5_Approved                           end as AFR5 
             , case when @approved = 0 then afr.AFRP1                          else afr.AFRP1_Approved                          end as AFRP1

             , case when @approved = 0 then r.ReinsuranceFlatfee1              else r.ReinsuranceFlatfee1_Approved              end as ReinsuranceFlatfee1
             , case when @approved = 0 then r.ReinsuranceFlatfee2              else r.ReinsuranceFlatfee2_Approved              end as ReinsuranceFlatfee2
             , case when @approved = 0 then r.ReinsuranceFlatfee3              else r.ReinsuranceFlatfee3_Approved              end as ReinsuranceFlatfee3
             , case when @approved = 0 then r.ReinsuranceFlatfee4              else r.ReinsuranceFlatfee4_Approved              end as ReinsuranceFlatfee4
             , case when @approved = 0 then r.ReinsuranceFlatfee5              else r.ReinsuranceFlatfee5_Approved              end as ReinsuranceFlatfee5
             , case when @approved = 0 then r.ReinsuranceFlatfeeP1             else r.ReinsuranceFlatfeeP1_Approved             end as ReinsuranceFlatfeeP1
             , case when @approved = 0 then r.ReinsuranceUpliftFactor_4h_24x7  else r.ReinsuranceUpliftFactor_4h_24x7_Approved  end as ReinsuranceUpliftFactor_4h_24x7
             , case when @approved = 0 then r.ReinsuranceUpliftFactor_4h_9x5   else r.ReinsuranceUpliftFactor_4h_9x5_Approved   end as ReinsuranceUpliftFactor_4h_9x5
             , case when @approved = 0 then r.ReinsuranceUpliftFactor_NBD_9x5  else r.ReinsuranceUpliftFactor_NBD_9x5_Approved  end as ReinsuranceUpliftFactor_NBD_9x5

        from InputAtoms.Wg wg 
        INNER JOIN InputAtoms.Pla pla on pla.id = wg.PlaId
        LEFT JOIN InputAtoms.Sog sog on sog.id = wg.SogId
        LEFT JOIN Hardware.AfrYear afr on afr.Wg = wg.Id
        LEFT JOIN Hardware.ReinsuranceYear r on r.Wg = wg.Id
        where wg.DeactivatedDateTime is null and (@wg is null or wg.Id = @wg)
    )
    , CostCte as (
            select 
                m.Id
                , m.CountryId
                , c.Name as Country
                , wg.Description as WgDescription
                , wg.Name as Wg
                , wg.SogDescription
                , wg.SCD_ServiceType
                , pro.ExternalName as Sla
                , loc.Name as ServiceLocation
                , rtime.Name as ReactionTime
                , rtype.Name as ReactionType
                , av.Name as Availability
                , c.Currency
                , c.ExchangeRate

                --FSP
                , fsp.Name Fsp
                , fsp.ServiceDescription as FspDescription

                --cost blocks

                , case when @approved = 0 then fsc.LabourCost else fsc.LabourCost_Approved end as LabourCost
                , case when @approved = 0 then fsc.TravelCost else fsc.TravelCost_Approved end as TravelCost
                , case when @approved = 0 then fst.PerformanceRate else fst.PerformanceRate_Approved end as PerformanceRate
                , case when @approved = 0 then fsc.TravelTime else fsc.TravelTime_Approved end as TravelTime
                , case when @approved = 0 then fsc.RepairTime else fsc.RepairTime_Approved end as RepairTime
                , case when @approved = 0 then hr.OnsiteHourlyRates else hr.OnsiteHourlyRates_Approved end as OnsiteHourlyRate


                , case when @approved = 0 then lc.StandardHandling else lc.StandardHandling_Approved end         as StandardHandling
                , case when @approved = 0 then lc.HighAvailabilityHandling else lc.HighAvailabilityHandling_Approved end as HighAvailabilityHandling 
                , case when @approved = 0 then lc.StandardDelivery else lc.StandardDelivery_Approved end         as StandardDelivery
                , case when @approved = 0 then lc.ExpressDelivery else lc.ExpressDelivery_Approved end          as ExpressDelivery
                , case when @approved = 0 then lc.TaxiCourierDelivery else lc.TaxiCourierDelivery_Approved end      as TaxiCourierDelivery
                , case when @approved = 0 then lc.ReturnDeliveryFactory else lc.ReturnDeliveryFactory_Approved end    as ReturnDeliveryFactory 
                , case when @approved = 0 then lc.StandardHandling else lc.StandardHandling_Approved end + case when @approved = 0 then lc.HighAvailabilityHandling else lc.HighAvailabilityHandling_Approved end as LogisticHandlingPerYear
                , case when @approved = 0 then lc.StandardDelivery else lc.StandardDelivery_Approved end + case when @approved = 0 then lc.ExpressDelivery else lc.ExpressDelivery_Approved end + case when @approved = 0 then lc.TaxiCourierDelivery else lc.TaxiCourierDelivery_Approved end + case when @approved = 0 then lc.ReturnDeliveryFactory else lc.ReturnDeliveryFactory_Approved end as LogisticTransportPerYear

                , case when afEx.id is not null then case when @approved = 0 then af.Fee else af.Fee_Approved end else 0 end as AvailabilityFee
      
                , c.TaxAndDuties as TaxAndDutiesW

                , case when @approved = 0 then moc.Markup else moc.Markup_Approved end       as MarkupOtherCost
                , case when @approved = 0 then moc.MarkupFactor else moc.MarkupFactor_Approved end as MarkupFactorOtherCost

                , case when @approved = 0 then msw.MarkupFactorStandardWarranty else msw.MarkupFactorStandardWarranty_Approved end as MarkupFactorStandardWarranty
                , case when @approved = 0 then msw.MarkupStandardWarranty else msw.MarkupStandardWarranty_Approved end       as MarkupStandardWarranty
      
                , wg.AFR1
                , wg.AFR2
                , wg.AFR3
                , wg.AFR4
                , wg.AFR5
                , wg.AFRP1

                , Hardware.CalcFieldServiceCost(
                            case when @approved = 0 then fst.TimeAndMaterialShare_norm else fst.TimeAndMaterialShare_norm_Approved end, 
                            case when @approved = 0 then fsc.TravelCost                else fsc.TravelCost_Approved end, 
                            case when @approved = 0 then fsc.LabourCost                else fsc.LabourCost_Approved end, 
                            case when @approved = 0 then fst.PerformanceRate           else fst.PerformanceRate_Approved end, 
                            case when @approved = 0 then fsc.TravelTime                else fsc.TravelTime_Approved end, 
                            case when @approved = 0 then fsc.RepairTime                else fsc.RepairTime_Approved end, 
                            case when @approved = 0 then hr.OnsiteHourlyRates          else hr.OnsiteHourlyRates_Approved end, 
                            1
                        ) as FieldServicePerYear

                , case when @approved = 0 then ssc.[1stLevelSupportCosts] else ssc.[1stLevelSupportCosts_Approved] end as [1stLevelSupportCosts]
                , case when @approved = 0 then ssc.[2ndLevelSupportCosts] else ssc.[2ndLevelSupportCosts_Approved] end as [2ndLevelSupportCosts]
           
                , wg.ReinsuranceFlatfee1
                , wg.ReinsuranceFlatfee2
                , wg.ReinsuranceFlatfee3
                , wg.ReinsuranceFlatfee4
                , wg.ReinsuranceFlatfee5
                , wg.ReinsuranceFlatfeeP1
                , wg.ReinsuranceUpliftFactor_4h_24x7
                , wg.ReinsuranceUpliftFactor_4h_9x5
                , wg.ReinsuranceUpliftFactor_NBD_9x5

                , case when @approved = 0 then mcw.MaterialCostIw else mcw.MaterialCostIw_Approved end as MaterialCostWarranty
                , case when @approved = 0 then mcw.MaterialCostOow else mcw.MaterialCostOow_Approved end as MaterialCostOow

                , dur.Value as Duration
                , dur.IsProlongation

        from Portfolio.GetBySlaSingle(@cnt, @wg, @av, @duration, @reactiontime, @reactiontype, @loc, @pro) m

        INNER JOIN CountryCte c on c.Id = m.CountryId

        INNER JOIN WgCte wg on wg.Id = m.WgId

        INNER JOIN Dependencies.Duration dur on dur.id = m.DurationId and dur.IsProlongation = 0

        INNER JOIN Dependencies.Availability av on av.Id= m.AvailabilityId

        INNER JOIN Dependencies.ReactionTime rtime on rtime.Id = m.ReactionTimeId

        INNER JOIN Dependencies.ReactionType rtype on rtype.Id = m.ReactionTypeId

        INNER JOIN Dependencies.ServiceLocation loc on loc.Id = m.ServiceLocationId

        INNER JOIN Dependencies.ProActiveSla pro on pro.Id = m.ProActiveSlaId

        LEFT JOIN Hardware.RoleCodeHourlyRates hr on hr.Country = m.CountryId and hr.RoleCode = wg.RoleCodeId 

        --cost blocks
        LEFT JOIN Hardware.FieldServiceCalc fsc ON fsc.Country = m.CountryId AND fsc.Wg = m.WgId AND fsc.ServiceLocation = m.ServiceLocationId
        LEFT JOIN Hardware.FieldServiceTimeCalc fst ON fst.Wg = m.WgId AND fst.Country = m.CountryId AND fst.ReactionTimeType = m.ReactionTime_ReactionType

        LEFT JOIN Hardware.LogisticsCosts lc on lc.Country = m.CountryId 
                                            AND lc.Wg = m.WgId
                                            AND lc.ReactionTimeType = m.ReactionTime_ReactionType
                                            AND lc.DeactivatedDateTime is null

        LEFT JOIN Hardware.MaterialCostWarrantyCalc mcw on mcw.Country = m.CountryId and mcw.Wg = m.WgId 

        LEFT JOIN Hardware.ServiceSupportCostView ssc on ssc.Country = m.CountryId and ssc.ClusterPla = wg.ClusterPlaId

        LEFT JOIN Hardware.MarkupOtherCosts moc on moc.Wg = m.WgId 
                                               AND moc.Country = m.CountryId 
                                               AND moc.ReactionTimeTypeAvailability = m.ReactionTime_ReactionType_Avalability 
                                               and moc.DeactivatedDateTime is null

        LEFT JOIN Hardware.MarkupStandardWaranty msw on msw.Country = m.CountryId AND msw.Wg = m.WgId and msw.DeactivatedDateTime is null

        LEFT JOIN Hardware.AvailabilityFeeCalc af on af.Country = m.CountryId AND af.Wg = m.WgId

        LEFT JOIN Admin.AvailabilityFee afEx on afEx.CountryId = m.CountryId 
                                            AND afEx.ReactionTimeId = m.ReactionTimeId 
                                            AND afEx.ReactionTypeId = m.ReactionTypeId 
                                            AND afEx.ServiceLocationId = m.ServiceLocationId

        LEFT JOIN Fsp.HwFspCodeTranslation fsp  on fsp.SlaHash = m.SlaHash and fsp.Sla = m.Sla
    )
    select    
                m.Id
              , m.Country
              , m.WgDescription
              , m.Wg
              , m.SogDescription
              , m.SCD_ServiceType
              , m.Sla
              , m.ServiceLocation
              , m.ReactionTime
              , m.ReactionType
              , m.Availability

             , m.Currency

             --FSP
              , m.Fsp
              , m.FspDescription

              --cost blocks

              , m.LabourCost as LabourCost
              , m.TravelCost as TravelCost
              , m.PerformanceRate as PerformanceRate
              , m.TravelTime
              , m.RepairTime
              , m.OnsiteHourlyRate as OnsiteHourlyRate

              , m.AvailabilityFee * m.ExchangeRate as AvailabilityFee
      
              , m.TaxAndDutiesW as TaxAndDutiesW

              , m.MarkupOtherCost as MarkupOtherCost
              , m.MarkupFactorOtherCost as MarkupFactorOtherCost

              , m.MarkupFactorStandardWarranty as MarkupFactorStandardWarranty
              , m.MarkupStandardWarranty as MarkupStandardWarranty
      
              , m.AFR1   * 100 as AFR1
              , m.AFR2   * 100 as AFR2
              , m.AFR3   * 100 as AFR3
              , m.AFR4   * 100 as AFR4
              , m.AFR5   * 100 as AFR5
              , m.AFRP1  * 100 as AFRP1

              , m.[1stLevelSupportCosts] * m.ExchangeRate as [1stLevelSupportCosts]
              , m.[2ndLevelSupportCosts] * m.ExchangeRate as [2ndLevelSupportCosts]
           
              , m.ReinsuranceFlatfee1 * m.ExchangeRate as ReinsuranceFlatfee1
              , m.ReinsuranceFlatfee2 * m.ExchangeRate as ReinsuranceFlatfee2
              , m.ReinsuranceFlatfee3 * m.ExchangeRate as ReinsuranceFlatfee3
              , m.ReinsuranceFlatfee4 * m.ExchangeRate as ReinsuranceFlatfee4
              , m.ReinsuranceFlatfee5 * m.ExchangeRate as ReinsuranceFlatfee5
              , m.ReinsuranceFlatfeeP1 * m.ExchangeRate as ReinsuranceFlatfeeP1
              , m.ReinsuranceUpliftFactor_4h_24x7 as ReinsuranceUpliftFactor_4h_24x7
              , m.ReinsuranceUpliftFactor_4h_9x5 as ReinsuranceUpliftFactor_4h_9x5
              , m.ReinsuranceUpliftFactor_NBD_9x5 as ReinsuranceUpliftFactor_NBD_9x5

              , m.MaterialCostWarranty * m.ExchangeRate as MaterialCostWarranty
              , m.MaterialCostOow * m.ExchangeRate as MaterialCostOow

              , m.Duration

              , m.FieldServicePerYear * m.AFR1 as FieldServiceCost1
              , m.FieldServicePerYear * m.AFR2 as FieldServiceCost2
              , m.FieldServicePerYear * m.AFR3 as FieldServiceCost3
              , m.FieldServicePerYear * m.AFR4 as FieldServiceCost4
              , m.FieldServicePerYear * m.AFR5 as FieldServiceCost5
            
              , m.StandardHandling
              , m.HighAvailabilityHandling
              , m.StandardDelivery
              , m.ExpressDelivery
              , m.TaxiCourierDelivery
              , m.ReturnDeliveryFactory 

              , Hardware.CalcByDur(
                      m.Duration
                    , m.IsProlongation 
                    , m.LogisticHandlingPerYear * m.AFR1 
                    , m.LogisticHandlingPerYear * m.AFR2 
                    , m.LogisticHandlingPerYear * m.AFR3 
                    , m.LogisticHandlingPerYear * m.AFR4 
                    , m.LogisticHandlingPerYear * m.AFR5 
                    , m.LogisticHandlingPerYear * m.AFRP1
                ) as LogisticsHandling

             , Hardware.CalcByDur(
                       m.Duration
                     , m.IsProlongation 
                     , m.LogisticTransportPerYear * m.AFR1 
                     , m.LogisticTransportPerYear * m.AFR2 
                     , m.LogisticTransportPerYear * m.AFR3 
                     , m.LogisticTransportPerYear * m.AFR4 
                     , m.LogisticTransportPerYear * m.AFR5 
                     , m.LogisticTransportPerYear * m.AFRP1
                 ) as LogisticTransportcost

    from CostCte m
)
go

IF OBJECT_ID('Report.spLocapGlobalSupport') IS NOT NULL
  DROP PROCEDURE Report.spLocapGlobalSupport;
go 

CREATE PROCEDURE [Report].[spLocapGlobalSupport]
(
    @approved bit,
    @cnt      dbo.ListID readonly,
    @sog      bigint,
    @wg       dbo.ListID readonly,
    @av       dbo.ListID readonly,
    @dur      dbo.ListID readonly,
    @rtime    dbo.ListID readonly,
    @rtype    dbo.ListID readonly,
    @loc      dbo.ListID readonly,
    @pro      dbo.ListID readonly,
    @lastid   int,
    @limit    int
)
AS
BEGIN

    if OBJECT_ID('tempdb..#tmp') is not null drop table #tmp;

    --Calc for Emeia countries by SOG

    declare @emeiaCnt dbo.ListID;
    declare @emeiaWg dbo.ListId;

    insert into @emeiaCnt(id)
    select c.Id
    from InputAtoms.Country c 
    join InputAtoms.ClusterRegion cr on cr.id = c.ClusterRegionId
    where exists (select * from @cnt where id = c.Id) and cr.IsEmeia = 1;

    insert into @emeiaWg
    select id
    from InputAtoms.Wg 
    where (@sog is null or SogId = @sog)
    and SogId in (select wg.SogId from InputAtoms.Wg wg  where (not exists(select 1 from @wg) or exists(select 1 from @wg where id = wg.Id)))
    and IsSoftware = 0
    and SogId is not null
    and DeactivatedDateTime is null;

    with cte as (
        select m.* 
                , case when m.IsProlongation = 1 then 'Prolongation' else CAST(m.Year as varchar(1)) end as ServicePeriod
        from Hardware.GetCostsSlaSog(1, @emeiaCnt, @emeiaWg, @av, @dur, @rtime, @rtype, @loc, @pro) m
        where (not exists(select 1 from @wg) or exists(select 1 from @wg where id = m.WgId))
    )
    , cte2 as (
        select    m.*
                , cnt.ISO3CountryCode
                , fsp.Name as Fsp
                , fsp.ServiceDescription as FspDescription

                , sog.SogDescription

        from cte m
        inner join InputAtoms.Country cnt on cnt.id = m.CountryId
        inner join InputAtoms.WgSogView sog on sog.Id = m.WgId
        left join Fsp.HwFspCodeTranslation fsp on fsp.SlaHash = m.SlaHash and fsp.Sla = m.Sla
    )
    select    c.Country
            , c.ISO3CountryCode
            , c.Fsp
            , c.FspDescription

            , c.SogDescription
            , c.Sog        
            , c.Wg

            , c.ServiceLocation
            , c.ReactionTime + ' ' + c.ReactionType + ' time, ' + c.Availability as ReactionTime
            , case when c.IsProlongation = 1 then 'Prolongation' else CAST(c.Year as varchar(1)) end as ServicePeriod
            , LOWER(c.Duration) + ' ' + c.ServiceLocation as ServiceProduct

            , c.LocalServiceStandardWarranty
            , case when @approved = 1 then c.ServiceTpSog_Approved else c.ServiceTpSog end as ServiceTP
            , c.DealerPrice
            , c.ListPrice
    into #tmp
    from cte2 c

    --Calc for non-Emeia countries by WG

    declare @nonEmeiaCnt dbo.ListID;
    declare @nonEmeiaWg dbo.ListID;

    insert into @nonEmeiaCnt(id)
    select c.Id
    from InputAtoms.Country c 
    join InputAtoms.ClusterRegion cr on cr.id = c.ClusterRegionId
    where exists (select * from @cnt where id = c.Id) and cr.IsEmeia = 0;

    insert into @nonEmeiaWg(id)
    select id
    from InputAtoms.Wg wg 
    where (not exists(select 1 from @wg) or exists(select 1 from @wg where id = wg.Id))
          and (@sog is null or wg.SogId = @sog)
          and IsSoftware = 0
          and DeactivatedDateTime is null;

    insert into #tmp
    select    c.Country
            , cnt.ISO3CountryCode
            , fsp.Name as Fsp
            , fsp.ServiceDescription as FspDescription

            , sog.SogDescription
            , c.Sog        
            , c.Wg

            , c.ServiceLocation
            , c.ReactionTime + ' ' + c.ReactionType + ' time, ' + c.Availability as ReactionTime
            , case when c.IsProlongation = 1 then 'Prolongation' else CAST(c.Year as varchar(1)) end as ServicePeriod
            , LOWER(c.Duration) + ' ' + c.ServiceLocation as ServiceProduct

            , c.LocalServiceStandardWarranty
            , case when @approved = 1 then c.ServiceTP else c.ServiceTP_Released end as ServiceTP
            , c.DealerPrice
            , c.ListPrice
    from Hardware.GetCosts(1, @nonEmeiaCnt, @nonEmeiaWg, @av, @dur, @rtime, @rtype, @loc, @pro, null, null) c
    inner join InputAtoms.Country cnt on cnt.id = c.CountryId
    inner join InputAtoms.WgSogView sog on sog.Id = c.WgId
    left join Fsp.HwFspCodeTranslation fsp on fsp.SlaHash = c.SlaHash and fsp.Sla = c.Sla;

    if @limit > 0
        select * from (
            select ROW_NUMBER() over(order by Country,Wg) as rownum, * from #tmp
        ) t
        where rownum > @lastid and rownum <= @lastid + @limit;
    else
        select * from #tmp order by Country,Wg;

END
GO